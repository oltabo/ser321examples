apply plugin: 'application'

mainClassName = 'Multiply'


// set as java application
//apply plugin: 'application'

// define main class (modern Gradle syntax)
//application {
//  mainClass = 'Multiply'
//}

// task which will run Multiply with default parameters,
// can be overwritten with: gradle runApp --args '3 4'
tasks.register('runApp', JavaExec) {
  group = 'Multiply tasks'
  description = 'Tasks which runs Multiply with default parameters'

  classpath = sourceSets.main.runtimeClasspath
  mainClass = 'Multiply'

  // default arguments if none are given
  args '1', '2'
}

// task which will run Multiply with default parameters,
// but also accepts new parameter this time with given names
// Example: gradle runAppAgain -Pnum1=9 -Pnum2=10
// Example2 (this will make Gradle run more quietly in the console):
//   gradle runAppAgain -Pnum1=9 -Pnum2=10 -q --console=plain
tasks.register('runAppAgain', JavaExec) {
  group = 'Multiply tasks'
  description = 'Tasks which runs Multiply with default parameters or given values'

  classpath = sourceSets.main.runtimeClasspath
  mainClass = 'Multiply'

  // only works if two arguments are provided
  // Try: using default values in case none or only one parameter is provided
  if (project.hasProperty('num1') && project.hasProperty('num2')) {
    args(project.getProperty('num1'), project.getProperty('num2'))
  } else if (project.hasProperty('num1')) {
    args(project.getProperty('num1'), '1')
  } else if (project.hasProperty('num2')) {
    args('1', project.getProperty('num2'))
  } else {
    args('1', '2')
  }
}

// task that runs the Fraction Main which does not use arguments: gradle runFraction
tasks.register('runFraction', JavaExec) {
  group = 'Fraction Tasks'
  description = 'Tasks which runs Fraction with no arguments'

  classpath = sourceSets.main.runtimeClasspath
  mainClass = 'Fraction'
}

// Example how you can add that libraries can be pulled from mavenCentral()
// repositories {
//   mavenCentral()
// }

// Setting dependencies, e.g. when you import JSON in your Java files
// dependencies {
//   implementation 'org.json:json:20171018'
// }
