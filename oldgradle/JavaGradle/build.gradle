plugins {
  id 'application'
}

repositories {
  // only needed if you add external dependencies later
  mavenCentral()
}

application {
  // main class for: ./gradlew run
  mainClass = 'Multiply'
}

// task which will run Multiply with default parameters,
// can be overwritten with: ./gradlew runApp --args "3 4"
tasks.register('runApp', JavaExec) {
  group = 'Multiply tasks'
  description = 'Task which runs Multiply with default parameters'

  classpath = sourceSets.main.runtimeClasspath
  mainClass = 'Multiply'

  // default arguments if none are given
  args '1', '2'
}

// task which will run Multiply with default parameters,
// but also accepts new parameter this time with given names
// Example: ./gradlew runAppAgain -Pnum1=9 -Pnum2=10
// Example2: ./gradlew runAppAgain -Pnum1=9 -Pnum2=10 -q --console=plain
tasks.register('runAppAgain', JavaExec) {
  group = 'Multiply tasks'
  description = 'Task which runs Multiply with default parameters or given values'

  classpath = sourceSets.main.runtimeClasspath
  mainClass = 'Multiply'

  def n1 = project.findProperty('num1')?.toString()
  def n2 = project.findProperty('num2')?.toString()

  if (n1 && n2) {
    args n1, n2
  } else if (n1) {
    args n1, '1'
  } else if (n2) {
    args '1', n2
  } else {
    args '1', '2'
  }
}

// task that runs the Fraction Main which does not use arguments: ./gradlew runFraction
tasks.register('runFraction', JavaExec) {
  group = 'Fraction Tasks'
  description = 'Task which runs Fraction with no arguments'

  classpath = sourceSets.main.runtimeClasspath
  mainClass = 'Fraction'
}

// If you need dependencies later (example):
// dependencies {
//   implementation 'org.json:json:20171018'
// }

